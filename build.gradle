buildscript {
    apply from: "gradle/versions.gradle"
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }

    }
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        jcenter()
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:$ktlint_version"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
    }

//check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }

    detekt {
        toolVersion = "$detekt_version"
        parallel = false
//        config = files("$projectDir/../config/detekt/config.yml")
        buildUponDefaultConfig = false
        // Interpret config files as updates to the default config. `false` by default.
        baseline = file("$projectDir/../config/detekt/baseline.xml")
        // Specifying a baseline file. All findings stored in this file in subsequent runs of detekt.
        disableDefaultRuleSets = false
        // Disables all default detekt rulesets and will only run detekt with custom rules defined in plugins passed in with `detektPlugins` configuration. `false` by default.
        debug = false
        // Adds debug output during task execution. `false` by default.
        ignoreFailures = false
        // If set to `true` the build does not fail when the maxIssues count was reached. Defaults to `false`.
        reports {
            xml {
                enabled = true                                // Enable/Disable XML report (default: true)
                destination = file("build/reports/detekt.xml")
                // Path where XML report will be stored (default: `build/reports/detekt/detekt.xml`)
            }
            html {
                enabled = true                                // Enable/Disable HTML report (default: true)
                destination = file("build/reports/detekt.html")
                // Path where HTML report will be stored (default: `build/reports/detekt/detekt.html`)
            }
            txt {
                enabled = true                                // Enable/Disable TXT report (default: true)
                destination = file("build/reports/detekt.txt")
                // Path where TXT report will be stored (default: `build/reports/detekt/detekt.txt`)
            }
            custom {
                reportId = "CustomJsonReport"                   // The simple class name of your custom report.
                destination = file("build/reports/detekt.json") // Path where report will be stored
            }
        }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
